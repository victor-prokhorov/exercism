# | Register | Usage        | Type    | Description                   |
# | -------- | ------------ | ------- | ----------------------------- |
# | `$a0`    | input        | address | null-terminated input string  |
# | `$a1`    | input/output | address | null-terminated output string |
# | `$t0-9`  | temporary    | any     | used for temporary storage    |

.globl response

.data

fine:           .asciiz "Fine. Be that way!"
calm:           .asciiz "Calm down, I know what I'm doing!"
chill:          .asciiz "Whoa, chill out!"
sure:           .asciiz "Sure."
whatever:       .asciiz "Whatever."

.text

response:
	li	$t1, 0		# found ascii lowercase character
	li	$t3, 0		# found ascii uppercase character
	li	$t5, 0		# address of last non whitespace character
        li      $t8, 0          # last non whitespace found character
loop:
	lb	$t6, ($a0)	# character to process
	addi	$a0, $a0, 1
	beqz	$t6, done
	beq	$t6, ' ', loop
	beq	$t6, '\n', loop
	beq	$t6, '\r', loop
	beq	$t6, '\t', loop
	lb	$t8, -1($a0)	# update with last non whitespace character
	subi	$t6, $t6, 'A'   # offset so 'A' is at index 0
	sltiu	$t7, $t6, 26	# is character contained in the range of ascii uppercase characters
        or      $t3, $t3, $t7
	subi 	$t6, $t6, 32    # change offset so 'a' is at index 0
	sltiu	$t7, $t6, 26	# is character contained in the range of ascii lowercase letters
        or      $t1, $t1, $t7
	j	loop
done:
	la	$t0, fine
	beqz	$t8, write	# if last non whitespace character still points to zero, the input is empty
	la	$t0, calm      
	seq	$t2, $t8, '?'   # last char was question mark
        nor     $t1, $t1, $zero
        and     $t7, $t1, $t3   # not found lowercase and found uppercase
        and     $t9, $t7, $t2   # and ends with question mark
        bnez    $t9, write
	la	$t0, chill
        bnez    $t7, write
	la	$t0, sure
        bnez    $t2, write
	la	$t0, whatever
write:
	lb	$t1, ($t0)
	addi	$t0, $t0, 1
	sb	$t1, ($a1)
	addi	$a1, $a1, 1
	bnez	$t1, write
	jr	$ra
